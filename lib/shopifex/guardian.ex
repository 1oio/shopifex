defmodule Shopifex.Guardian do
  @secret Application.compile_env!(:shopifex, :secret)
  @app_name Application.compile_env!(:shopifex, :app_name)

  use Guardian,
    otp_app: :shopifex,
    issuer: @app_name,
    secret_key: @secret,
    allowed_algos: ["HS256", "HS512"]

  def subject_for_token(%{url: url}, _claims), do: {:ok, url}

  @doc """
  session_token tokens from the ShopifySession plug
  will contain the shop URL in a dest claim. This is the
  token which is generated by Shopify App Bridge in
  load-session.js
  """
  def resource_from_claims(%{"dest" => dest_claim}) do
    shop_url =
      dest_claim
      |> URI.parse()
      |> Map.get(:host)

    shop = Shopifex.Shops.get_shop_by_url(shop_url)
    {:ok, shop}
  end

  @doc """
  Since app bridge tokens are only short lived, we generate
  a new longer lived token for the rest of the session
  lifetime. These tokens contain the shop url in the
  "sub" claim.
  """
  def resource_from_claims(%{"sub" => shop_url}) do
    shop = Shopifex.Shops.get_shop_by_url(shop_url)
    {:ok, shop}
  end

  def resource_from_claims(_claims) do
    {:error, :reason_for_error}
  end
end
